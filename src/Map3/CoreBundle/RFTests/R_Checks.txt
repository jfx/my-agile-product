*** Keywords ***
I check the following ACL
    [Arguments]    ${acl_url}    ${acl_super-admin}    ${acl_manager}    ${acl_user+}    ${acl_user}    ${acl_guest}
    ...    ${acl_none}    ${acl_noproduct}    ${acl_prerequisite}=False
    ${isPrerequiste} =    Set Variable If    '${acl_prerequisite}' == 'False'    False    True
    @{acls}=    Create List    ${acl_super-admin}    ${acl_manager}    ${acl_user+}    ${acl_user}    ${acl_guest}
    ...    ${acl_none}    ${acl_noproduct}
    @{users}=    Create List    ${USER_ADMIN}    ${USER_MANAGER}    ${USER_USER+}    ${USER_USER}    ${USER_GUEST}
    ...    ${USER_NONE}    ${USER_NOPRODUCT}
    ${i} =    Set Variable    0
    : FOR    ${user}    IN    @{users}
    \    I am logging as    ${user}
    \    Run Keyword If    ${isPrerequiste}    Go to    ${URL}${acl_prerequisite}
    \    Go to    ${URL}${acl_url}
    \    ${acl} =    Get From List    ${acls}    ${i}
    \    Run Keyword If    ${acl}    Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}
    \    ...    ELSE    Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    \    I log out
    \    ${i} =    Evaluate    ${i} + 1

I check the following ACL by request
    [Arguments]    ${acl_url}    ${acl_super-admin}    ${acl_manager}    ${acl_user+}    ${acl_user}    ${acl_guest}
    ...    ${acl_none}    ${acl_noproduct}    ${acl_prerequisite}=False
    ${isPrerequiste} =    Set Variable If    '${acl_prerequisite}' == 'False'    False    True
    @{acls}=    Create List    ${acl_super-admin}    ${acl_manager}    ${acl_user+}    ${acl_user}    ${acl_guest}
    ...    ${acl_none}    ${acl_noproduct}
    @{users}=    Create List    ${USER_ADMIN}    ${USER_MANAGER}    ${USER_USER+}    ${USER_USER}    ${USER_GUEST}
    ...    ${USER_NONE}    ${USER_NOPRODUCT}
    ${i} =    Set Variable    0
    : FOR    ${user}    IN    @{users}
    \    I am logging as by request    ${user}
    \    Run Keyword If    ${isPrerequiste}    Ajax Get Request    ${acl_prerequisite}
    \    ${resp}=    Ajax Get Request    ${acl_url}
    \    ${acl} =    Get From List    ${acls}    ${i}
    \    Run Keyword If    ${acl}    Then Should Be Equal As Strings    ${resp.status_code}    200
    \    ...    ELSE    Then Should Be Equal As Strings    ${resp.status_code}    403
    \    ${i} =    Evaluate    ${i} + 1

the table should contain x rows
    [Arguments]    ${count}
    ${r} =    Evaluate    ${count} + 1
    And Xpath Should Match X Times    //tr    ${r}

the table should contain
    [Arguments]    @{list}
    : FOR    ${item}    IN    @{list}
    \    Table Should Contain    xpath=//table    ${item}

the table header should contain
    [Arguments]    @{list}
    : FOR    ${item}    IN    @{list}
    \    Table Header Should Contain    xpath=//table    ${item}

I should see action button(s)
    [Arguments]    @{list}
    : FOR    ${item}    IN    @{list}
    \    Element Should Be Visible    xpath=//a[@title='${item}']

I should not see action button(s)
    [Arguments]    @{list}
    : FOR    ${item}    IN    @{list}
    \    Page Should Not Contain Element    xpath=//a[@title='${item}']

the action button should be hidden
    [Arguments]    ${button}
    Page Should Contain Element    xpath=//a[@title='${button}']
    Element Should Not Be Visible    xpath=//a[@title='${button}']

I should see 404 error page
    [Arguments]    ${msg}='404 Item not found'
    Page Should Contain    404 Not Found

Disabled text element should contain
    [Arguments]    ${id}    ${expected}
    Element Should Be Disabled    ${id}
    ${value} =    Selenium2Library.Get Element Attribute    ${id}@value
    Should Be Equal    ${expected}    ${value}

the breadcrumb should contain
    [Arguments]    @{list}
    ${i} =    Set variable    0
    : FOR    ${item}    IN    @{list}
    \    ${i} =    Evaluate    ${i} + 1
    \    ${level} =    Get Text    br_lvl${i}
    \    Should Be Equal As Strings    ${item}    ${level}

the context log file should contain x lines
    [Arguments]    ${count}
    ${file}=    Get File    ${LOG_PATH}/uctx_dev.log
    ${count_lines}=    Get Line Count    ${file}
    Should Be Equal As Numbers    ${count_lines}    ${count}

the context log file should contain
    [Arguments]    @{list}
    ${file}=    Get File    ${LOG_PATH}/uctx_dev.log
    : FOR    ${item}    IN    @{list}
    \    Should Contain    ${file}    ${item}

the context log file should not contain
    [Arguments]    @{list}
    ${file}=    Get File    ${LOG_PATH}/uctx_dev.log
    : FOR    ${item}    IN    @{list}
    \    Should Not Contain    ${file}    ${item}

the element should contain items
    [Arguments]    ${element}    @{list}
    : FOR    ${item}    IN    @{list}
    \    Should Contain    ${element}    ${item}
    \    log    ${item} found !

the node name should be equal to
    [Arguments]    ${id}    ${name}
    Wait Until Page Contains Element    id=${id}_anchor    ${WAIT_TIMEOUT}
    ${node_name} =    Get Text    id=${id}_anchor
    Should Be Equal As Strings    ${node_name}    ${name}

I wait until text is visible
    [Arguments]    ${message}
    Wait Until Page Contains    ${message}    ${WAIT_TIMEOUT}
    Page Should Contain    ${message}

the label of this field is displayed as required
    [Arguments]    ${label}
    ${html}=    Get Source
    ${line}=    Get Lines Containing String    ${html}    ${label}
    ${count}=    Get Line Count    ${line}
    Run Keyword And Expect Error    *    Should Be Equal    ${count}    0
    Should Contain    ${line}    <span class="asterisk">*</span>

the label of this field is displayed as optional
    [Arguments]    ${label}
    ${html}=    Get Source
    ${line}=    Get Lines Containing String    ${html}    ${label}
    ${count}=    Get Line Count    ${line}
    Run Keyword And Expect Error    *    Should Be Equal    ${count}    0
    Should Not Contain    ${line}    <span class="asterisk">*</span>

the color of the icon is
    [Arguments]    ${id}    ${icon}    ${color}
    Wait Until Element Is Visible    ${id}_anchor    ${WAIT_TIMEOUT}
    ${count} =    Get Matching Xpath Count    //a[@id='${id}_anchor']/i[@class='jstree-icon jstree-themeicon glyphicon glyphicon-${icon} ${color} jstree-themeicon-custom']
    Should Be Equal    ${count}    1

the color of the icon is black
    [Arguments]    ${id}    ${icon}
    Wait Until Element Is Visible    ${id}_anchor    ${WAIT_TIMEOUT}
    ${count} =    Get Matching Xpath Count    //a[@id='${id}_anchor']/i[@class='jstree-icon jstree-themeicon glyphicon glyphicon-${icon} jstree-themeicon-custom']
    Should Be Equal    ${count}    1
