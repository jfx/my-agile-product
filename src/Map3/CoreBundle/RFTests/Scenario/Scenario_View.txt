*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
Click to view a scenario node
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    When I click node    S_2
    Then I check scenario view form    S 2 F1 - Unchecked    SC-002    Unchecked    Given some precondition\nAnd some other precondition\nWhen some action by the actor\nAnd some other action\nAnd yet another action\nThen some testable outcome is achieved\nAnd something else we can check happens too

Scenario steps have syntax highlight
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    When I click node    S_2
    And Wait Until Element Is Visible    map3_scenario_title    ${WAIT_TIMEOUT}
    Then I see steps displayed with syntax highlight

Reload warning after timeout when click to view scenario
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    F_6
    When I am waiting for timeout
    And I click node    S_15
    Then I wait until text is visible    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request view scenario
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    S 1 F1 - Not implemented    Given some precondition
    [Teardown]    EnvPostrequisite

Request view scenario with non numeric Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_XXX
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong node Id
    [Teardown]    EnvPostrequisite

Request view scenario with wrong node Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request view scenario with wrong baseline Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/999/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    S 1 F1 - Not implemented
    [Teardown]    EnvPostrequisite

Request view scenario timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    user    user    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for scenario ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Edit button displayed for user profile
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_1
    When I click button when visible    Edit
    And I am waiting for ajax request
    Then Location Should Be    ${URL}/bln-features/1
    And I should see action button(s)    Save

Reload warning after timeout when click edit button
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_1
    And I am waiting for timeout
    When I click button    Edit
    Then I wait until text is visible    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Edit button not displayed for closed baseline
    Given I am a user
    And I go directly to features tree    3
    And the node name should be equal to    B_7    Baseline Closed
    And I click node to display sub-node    B_7
    And I click node to display sub-node    F_7
    When I click node    S_16
    Then I check scenario view form    S 16 F0 - closed    SC-010    Passed    Given some precondition\nAnd some other precondition\nWhen some action by the actor\nAnd some other action\nAnd yet another action\nThen some testable outcome is achieved\nAnd something else we can check happens too
    And I should not see action button(s)    Edit

Edit button not displayed for non user profile
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_1
    Then I check scenario view form    S 1 F1 - Not implemented    ${EMPTY}    Not implemented    Given some precondition\nAnd some other precondition\nWhen some action by the actor\nAnd some other action\nAnd yet another action\nThen some testable outcome is achieved\nAnd something else we can check happens too
    And I should not see action button(s)    Edit

Icon of a scenario with "Not implemented" status should be grey lighter
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is    S_1    film    color-disabled

Icon of a scenario with "Unchecked" status should be grey lighter
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is    S_2    film    color-disabled

Icon of a scenario with "Pending" status should be black (default)
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is black    S_3    film

Icon of a scenario with "Passed" status should be green
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is    S_4    film    text-success

Icon of a scenario with "Failed" status should be red
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is    S_5    film    text-danger

Icon of a scenario with "Undefined" status should be black (default)
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    When I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    Then the color of the icon is black    S_6    film
