*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user adds a scenario
    [Setup]    TestSetupDb
    Given I am a user
    And Check If Not Exists In Database    select id from map3_scenario where feature_id = 1 and position = 12
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    And I Click button    Add scenario
    When I fill in scenario form    Scenario added    S001-Added    Unchecked    Given 1 When 2 Then 3
    And I click button    Save
    Then I wait until text is visible    Scenario added successfully
    And I check scenario view form    Scenario added    S001-Added    Unchecked    Given 1 When 2 Then 3
    And Check If Exists In Database    select id from map3_scenario where feature_id = 1 and position = 12
    [Teardown]    TestTeardownDb

A user adds a scenario with default values
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    And I Click button    Add scenario
    When Wait Until Element Is Visible    map3_scenario_title    ${WAIT_TIMEOUT}
    And List Selection Should Be    map3_scenario_status    Pending
    ${value}=    Get Value    map3_scenario_steps
    And Should Be Equal    ${value}    Given [Pre-condition]\nWhen [Action]\nThen [Outcome]
    And Input text    map3_scenario_title    Scenario added
    And I click button    Save
    Then I wait until text is visible    Scenario added successfully
    And I check scenario view form    Scenario added    ${EMPTY}    Pending    Given [Pre-condition]\nWhen [Action]\nThen [Outcome]

Scenario steps have syntax highlight
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    And I Click button    Add scenario
    When Wait Until Element Is Visible    map3_scenario_title    ${WAIT_TIMEOUT}
    Then I see steps displayed with syntax highlight

Reload warning after timeout when click Add scenario
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    And I am waiting for timeout
    And I Click button    Add scenario
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Reload warning after timeout when click Save
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    And Click Element    addMenu
    And I Click button    Add scenario
    When I fill in scenario form    Scenario added    S001-Added    Unchecked    Given 1 When 2 Then 3
    And I am waiting for timeout
    And I click button    Save
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request add scenario
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Add scenario
    [Teardown]    EnvPostrequisite

Impossible to add a scenario for a closed baseline
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_7
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the element should contain items    ${resp.content}    You are not allowed to access this resource
    [Teardown]    EnvPostrequisite

Request add scenario with non existent parent feature Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request add scenario with non existent type of parent node
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/X_1
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong type of node
    [Teardown]    EnvPostrequisite

Request add scenario with multiple selected nodes
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1,F_2
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Please, select only one node !
    [Teardown]    EnvPostrequisite

Request add scenario with not allowed parent node - baseline
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/B_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add scenario with not allowed parent node - category
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add scenario with not allowed parent node - scenario
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-sce/add/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add scenario timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    user    user    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for scenario ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for scenario ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-sce/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Required and optional form labels
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    And Click Element    addMenu
    Then I Click button    Add scenario
    And I wait until button is visible    Save
    Then the label of this field is displayed as required    Title
    And the label of this field is displayed as optional    Ext id
    Then the label of this field is displayed as required    Status
    And the label of this field is displayed as optional    Steps

Impossible to add a scenario with an empty title
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    And Click Element    addMenu
    And I Click button    Add scenario
    When I fill in scenario form    ${EMPTY}    S001-Added    Unchecked    Given 1 When 2 Then 3
    And I click button    Save
    Then I wait until text is visible    This value should not be blank.

Impossible to add a scenario with a title too short
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    And Click Element    addMenu
    And I Click button    Add scenario
    When I fill in scenario form    X    S001-Added    Unchecked    Given 1 When 2 Then 3
    And I click button    Save
    Then I wait until text is visible    This value is too short. It should have 2 characters or more.

Add scenario item menu for a user when a feature node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    Then element should be visible    addSceBt

No add scenario item menu displayed for a user when a baseline node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node    B_1
    When Click Element    addMenu
    Then element should not be visible    addSceBt

No add scenario item menu displayed for a user when a category node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node    C_9
    When Click Element    addMenu
    Then element should not be visible    addSceBt

No add scenario item menu for a user when a scenario node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_1
    When Click Element    addMenu
    Then element should not be visible    addSceBt

No add button for a guest when a feature node is selected
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    When I click node    F_1
    Then Page Should Not Contain Element    addMenu
