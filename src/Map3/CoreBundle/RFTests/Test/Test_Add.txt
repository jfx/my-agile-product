*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user adds a skipped test
    [Setup]    TestSetupDb
    Given I am a user
    And Check If Not Exists In Database    select id from map3_test where scenario_id = 3
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I Click button    Add test
    And I fill in test add form    15    D1-user+ Firstd1-user+    Test1 S3 added
    And I select result at index    1    Passed
    And I click button    Save
    Then I wait until text is visible    Test added successfully
    And I check test view form    15    D1-user+ Firstd1-user+    Test1 S3 added    0
    And the step result should be    1    Given some precondition    Passed
    And the color of the icon is black    T_14    question-sign
    And Check If Exists In Database    select id from map3_test where scenario_id = 3
    [Teardown]    TestTeardownDb

A user adds a failed test
    [Setup]    TestSetupDb
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I Click button    Add test
    And I fill in test add form    15    D1-user+ Firstd1-user+    Test1 S3 added
    And I select result at index    1    Failed
    And I click button    Save
    Then I wait until text is visible    Test added successfully
    And I check test view form    15    D1-user+ Firstd1-user+    Test1 S3 added    0
    And the step result should be    1    Given some precondition    Failed
    And the color of the icon is    T_14    remove-sign    text-danger
    [Teardown]    TestTeardownDb

A user adds a passed test
    [Setup]    TestSetupDb
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I Click button    Add test
    And I fill in test add form    15    D1-user+ Firstd1-user+    Test1 S3 added
    And I select result at index    1    Passed
    And I select result at index    2    Passed
    And I select result at index    3    Passed
    And I select result at index    4    Passed
    And I select result at index    5    Passed
    And I select result at index    6    Passed
    And I select result at index    7    Passed
    And I click button    Save
    Then I wait until text is visible    Test added successfully
    And I check test view form    15    D1-user+ Firstd1-user+    Test1 S3 added    0
    And the step result should be    1    Given some precondition    Passed
    And the step result should be    2    And some other precondition    Passed
    And the step result should be    3    When some action by the actor    Passed
    And the step result should be    4    And some other action    Passed
    And the step result should be    5    And yet another action    Passed
    And the step result should be    6    Then some testable outcome is achieved    Passed
    And the step result should be    7    And something else we can check happens too    Passed
    And the color of the icon is    T_14    ok-sign    text-success
    [Teardown]    TestTeardownDb

Reload warning after timeout when click Add test
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I am waiting for timeout
    And I Click button    Add test
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Reload warning after timeout when click Save
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I Click button    Add test
    And I fill in test add form    15    D1-user+ Firstd1-user+    Test1 S3 added
    And I am waiting for timeout
    And I click button    Save
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request add test
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Add test
    [Teardown]    EnvPostrequisite

Impossible to add a test for a closed baseline
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/S_16
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the element should contain items    ${resp.content}    You are not allowed to access this resource
    [Teardown]    EnvPostrequisite

Request add test with non existent parent feature Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/S_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request add test with non existent type of parent node
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/X_1
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong type of node
    [Teardown]    EnvPostrequisite

Request add test with multiple selected nodes
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/S_1,S_2
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Please, select only one node !
    [Teardown]    EnvPostrequisite

Request add test with not allowed parent node - baseline
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/B_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add test with not allowed parent node - category
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add test with not allowed parent node - feature
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add test with not allowed parent node - test
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-test/add/T_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add test timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    user    user    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for test ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for test ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for test ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for test ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for test ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for test ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-test/add/S_3
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Required and optional form labels
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    And I Click button    Add test
    And I wait until button is visible    Save
    Then the label of this field is displayed as required    Date
    And the label of this field is displayed as optional    Tester
    And the label of this field is displayed as optional    Comment

Impossible to add a test with an empty date
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    And Click Element    addMenu
    And I Click button    Add test
    When I click button when visible    Save
    Then I wait until text is visible    This value is not a valid datetime

Add test item menu for a user when a scenario node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node    S_3
    When Click Element    addMenu
    Then element should be visible    addTesBt

No add test item menu displayed for a user when a baseline node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node    B_1
    When Click Element    addMenu
    Then element should not be visible    addTesBt

No add test item menu displayed for a user when a category node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node    C_9
    When Click Element    addMenu
    Then element should not be visible    addTesBt

No add test item menu for a user when a feature node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1
    When Click Element    addMenu
    Then element should not be visible    addTesBt

No add test item menu for a user when a test node is selected
    Given I am a user
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    And I click node to display sub-node    S_4
    And I click node    T_1
    When Click Element    addMenu
    Then element should not be visible    addSceBt

No add button for a guest when a scenario node is selected
    Given I am a guest
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node to display sub-node    F_1
    When I click node    S_3
    Then Page Should Not Contain Element    addMenu
