*** Keywords ***
I open browser
    Open Browser    ${LOGIN_URL}    ${BROWSER}    \    ${HUB}
    Maximize Browser Window
    Run Keyword If    '${ENV}' == 'dev'    Wait Until Page Contains Element    xpath=//a[@title='Close Toolbar']
    Run Keyword If    '${ENV}' == 'dev'    Click element    xpath=//a[@title='Close Toolbar']

I am logging as
    [Arguments]    ${user}    ${password}=DEFAULT    ${check}=Hello    ${force_dev}=False
    ${close_toolbar} =     Convert to Boolean    'False'
    Run Keyword If    ${force_dev}    Go to    ${URL_DEV}/login
    ${close_toolbar} =     Set Variable if    '${ENV}' != 'dev'    ${force_dev}
    Run Keyword If    ${close_toolbar}    Wait Until Page Contains Element    xpath=//a[@title='Close Toolbar']
    Run Keyword If    ${close_toolbar}    Click element    xpath=//a[@title='Close Toolbar']
    Run Keyword Unless    ${force_dev}    Go to    ${LOGIN_URL}
    ${password}    Set Variable If    '${password}' == 'DEFAULT'    ${user}    ${password}
    Input text    username    user${user}
    Input text    password    ${password}
    Click element    xpath=//button[@title='Login']
    Page Should Contain    ${check}

I am logging as by request
    [Arguments]    ${user}    ${password}=DEFAULT    ${check}=Hello    ${force_dev}=False
    Run Keyword If    ${force_dev}    create session    ${SESSION}    ${URL_DEV}
    Run Keyword Unless    ${force_dev}    create session    ${SESSION}    ${URL}
    ${password}    Set Variable If    '${password}' == 'DEFAULT'    ${user}    ${password}
    ${resp}=    get request    ${SESSION}    /login
    ${line}=    Get Lines Containing String    ${resp.content}    _csrf_token
    ${token}=    XML.Get Element Attribute    ${line}    value    xpath=.
    ${data}=    Create Dictionary    _csrf_token=${token}    _username=user${user}    _password=${password}
    ${headers}=    Create Dictionary    Content-Type    application/x-www-form-urlencoded
    ${resp}=    Post request    ${SESSION}    /login_check    data=${data}    headers=${headers}
    Should Be Equal As Strings    ${resp.status_code}    200
    Should Contain    ${resp.content}    ${check}

I am a super-admin
    Run Keyword    I am logging as    ${USER_ADMIN}

I am a manager
    Run Keyword    I am logging as    ${USER_MANAGER}

I am a user+
    Run Keyword    I am logging as    ${USER_USER+}

I am a user
    Run Keyword    I am logging as    ${USER_USER}

I am a guest
    Run Keyword    I am logging as    ${USER_GUEST}

I am a user with no role
    Run Keyword    I am logging as    ${USER_NONE}

I am a user with no role on any product
    Run Keyword    I am logging as    ${USER_NOPRODUCT}

I go directly to features tree
    [Arguments]    ${baselineId}
    Go to    ${URL}/bln-features/${baselineId}

I log out
    Go to    ${URL}/logout

I am waiting for timeout
    sleep    ${WAIT_TIMEOUT}

I am waiting for ajax request
    [Arguments]    ${x}=1
    Repeat Keyword    ${x}    sleep    ${WAIT_AJAX}

TestSetup
    EnvPrerequisite
    I open browser

TestTeardown
    EnvPostrequisite
    Close All Browsers

TestSetupDb
    Connect To Database    MySQLdb    map3dev    map3dev    map3dev    127.0.0.1    3306
    TestSetup

TestTeardownDb
    Disconnect from Database
    TestTeardown

TestLifetimeSetup
    SetLifetime
    TestSetup

TestLifetimeTeardown
    TestTeardown
    UnsetLifetime
