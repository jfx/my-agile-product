*** Settings ***
Documentation     In order to see details about a specific baseline
...               As a connected user
...               I need to view baseline details.
Resource          ../Keyword_resource.txt

*** Test Cases ***
View baseline details
    Given I am a user
    And I go to view baseline page    3
    Then Location Should Be    ${URL}/baseline/3
    And Disabled text element should contain    map3_baseline_name    Baseline Closed
    And Disabled text element should contain    map3_baseline_baselineDatetime    2014-08-02 12:00
    And Checkbox Should Be Selected    map3_baseline_closed

Check context for baseline view with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for baseline view without context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user

Check context for baseline view with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And Go To    ${URL_DEV}/baseline/6
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Update user    Role : ROLE_DM_NONE

Check context for baseline view with same previous product
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user

Check context for baseline view with same previous release
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/2
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for baseline view with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Wrong baseline Id for a release but same product
    Given I am a user
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/4
    Then Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}
    And Page Should Contain    Release Two

Wrong baseline Id for a release
    Given I am a user
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/6
    Then Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}
    And Page Should Contain    Release Three

Wrong baseline Id
    Given I am a user
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/999
    Then I should see 404 error page

View a baseline without selecting a product before
    Given I am a user
    When Go to    ${URL}/baseline/1
    Then Location should be    ${URL}/baseline/1
    And the breadcrumb should contain    Product One    Release One    Baseline One    View
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a user
    When I go to view baseline page    1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    View

Breadcrumb product link
    Given I am a user
    And I go to view baseline page    1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user
    And I go to view baseline page    1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user
    And I go to view baseline page    1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user
    And I go to view baseline page    1
    When Click Link    id=a_main
    And Location Should Be    ${URL}/baseline/1

Tab Features
    Given I am a user
    And I go to view baseline page    1
    And Element Should Contain    id=badge_features    6
    When Click element    id=a_features
    And Location Should Be    ${URL}/bln-features/1

Tab References
    Given I am a user
    And I go to view baseline page    1
    And Element Should Contain    id=badge_references    3
    When Click element    id=a_references
    And Location Should Be    ${URL}/bln-refs/1

Return to release button
    Given I am a user
    And I go to view baseline page    1
    When I click button    Return to release
    Then Location Should Be    ${URL}/baselines/1

Edit button displayed for user+ profile
    Given I am a user+
    And I go to view baseline page    1
    When I click button    Edit
    Then Location Should Be    ${URL}/baseline/edit/1

Edit button not displayed for closed release
    Given I am a user
    When I go to view baseline page    3
    Then I should not see action button(s)    Edit

Edit button not displayed for non user+ profile
    Given I am a user
    When I go to view baseline page    1
    Then I should not see action button(s)    Edit

*** Keywords ***
I go to view baseline page
    [Arguments]    ${id}
    I go to baselines list    View product #1    View release #1
    I click action button    View baseline #${id}
