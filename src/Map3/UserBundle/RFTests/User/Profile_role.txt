*** Settings ***
Documentation     In order to see my role on products
...               As a connected user
...               I need to see my different role on each product
Resource          ../Keyword_resource.txt

*** Test Cases ***
Multiple roles
    Given I am a user
    When I go to profile role page
    Then the table should contain x rows    2
    And the table should contain    Product One    User
    And the table should contain    Product Two    User+

No role
    Given I am a super-admin
    When I go to profile role page
    Then the table should contain x rows    1
    And the table should contain    No role

Check context for user role with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/user/role
    Then the breadcrumb should contain    Admin    Profile    Roles
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->unsetCurrentProduct    Reset childs : Release and above    User->unsetCurrentProduct    Update user

Breadcrumb
    Given I am a user
    When I go to profile role page
    the breadcrumb should contain    Admin    Profile    Roles

Main tab
    Given I am a user
    And I go to profile role page
    When Click Link    Main
    Then Location Should Be    ${URL}/user/profile

Password tab
    Given I am a user
    And I go to profile role page
    When Click Link    Password
    Then Location Should Be    ${URL}/user/password

*** Keywords ***
I go to profile role page
    Click element    link=Admin
    Click element    link=Profile
    Click element    link=Roles
