*** Settings ***
Documentation     In order to use the application
...               As a anonymous user
...               I need to reset my password
Resource          ../Keyword_resource.txt

*** Test Cases ***
Reset password by username
    [Tags]    gmail
    Given Go to    ${URL_DEV}/login
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    useruser
    And Location Should Be    ${URL_DEV}/resetting/check-email?email=...%40example.com
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And I am logging into Gmail
    And I select unread email with subject    My Agile Project : Resetting your password
    And I click link in message
    And I fill in reset form step 2    changedPass    changedPass
    And Click element    xpath=//button[@title='Change']
    Then Page Should Contain    The password has been reset successfully
    And I check login    user    changedPass

Reset password by email
    [Tags]    gmail
    Given Go to    ${URL_DEV}/login
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    user@example.com
    And Location Should Be    ${URL_DEV}/resetting/check-email?email=...%40example.com
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And I am logging into Gmail
    And I select unread email with subject    My Agile Project : Resetting your password
    And I click link in message
    And I fill in reset form step 2    changedPass    changedPass
    And Click element    xpath=//button[@title='Change']
    Then Page Should Contain    The password has been reset successfully
    And I check login    user    changedPass

Email content
    [Tags]    gmail
    Given Go to    ${URL_DEV}/login
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    useruser
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And I am logging into Gmail
    And I select unread email with subject    My Agile Project : Resetting your password
    Then Page Should Contain    Hello Firstuser User
    And Page Should Contain    You can reset your password by accessing
    And Page Should Contain    My Agile Project Password Management

Step 1 Password already requested
    Given Go to    ${LOGIN_URL}
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    useruser
    And Location Should Be    ${URL}/resetting/check-email?email=...%40example.com
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And Go to    ${LOGIN_URL}
    And Click Link    Forgot your password ?
    And I fill in reset form step 1    useruser
    Then Location Should Be    ${URL}/resetting/send-email
    And Page Should Contain    The password for this user has already been requested within the last 24 hours

Step 1 Wrong id
    Given Go to    ${LOGIN_URL}
    And Click Link    Forgot your password ?
    When Input Text    username    X
    And Click element    xpath=//button[@title='Reset']
    Then Location Should Be    ${URL}/resetting/send-email
    And Page Should Contain    The username or email address "X" does not exist.

Step 1 Login link
    Given Go to    ${LOGIN_URL}
    And Click Link    Forgot your password ?
    When Click Link    Return to login
    Then Location Should Be    ${URL}/login

Step 2 Wrong confirmation token
    Given Go to    ${LOGIN_URL}
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    useruser
    And Location Should Be    ${URL}/resetting/check-email?email=...%40example.com
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And Go to    ${URL}/resetting/reset/AVopDxQJHwNHHauw0U7WNp-SgrAqWVDA4IvUNDirzjk
    Then I should see 404 error page    The user with "confirmation token" does not exist for value

Step 2 Reset passwords do not match
    [Tags]    gmail
    Given Go to    ${URL_DEV}/login
    And Click Link    Forgot your password ?
    When I fill in reset form step 1    useruser
    And Location Should Be    ${URL_DEV}/resetting/check-email?email=...%40example.com
    And Page Should Contain    An email has been sent to ...@example.com. It contains a link you must click to reset your password.
    And I am logging into Gmail
    And I select unread email with subject    My Agile Project : Resetting your password
    And I click link in message
    And I fill in reset form step 2    changedPass    wrongPass
    And Click element    xpath=//button[@title='Change']
    Then Page Should Contain    The entered passwords don't match
    And I am logging as    user

*** Keywords ***
I fill in reset form step 1
    [Arguments]    ${id}
    the breadcrumb should contain    My Agile Project    Resetting password    Step 1
    Input Text    username    ${id}
    Click element    xpath=//button[@title='Reset']

I fill in reset form step 2
    [Arguments]    ${password1}    ${password2}
    sleep    1
    Select Window    title=My Agile Project - Resetting password - Step 2
    the breadcrumb should contain    My Agile Project    Resetting password    Step 2
    Input Text    fos_user_resetting_form_plainPassword_first    ${password1}
    Input Text    fos_user_resetting_form_plainPassword_second    ${password2}

I check login
    [Arguments]    ${user}    ${password}
    Go to    ${URL_DEV}/login
    Input text    username    user${user}
    Input text    password    ${password}
    Click element    xpath=//button[@title='Login']
    the breadcrumb should contain    Admin    Profile
