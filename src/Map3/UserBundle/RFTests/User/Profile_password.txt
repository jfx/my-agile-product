*** Settings ***
Documentation     In order to change my password
...               As a connected user
...               I need to submit my old password and the new one.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Change password
    Given I am a user
    And I go to profile password page
    When I fill in password form    user    passChanged    passChanged
    And I save
    Then Page Should Contain    Password modified
    And I log out
    And I am logging as    user    passChanged
    Then Page Should Contain    Hello Firstuser User !

Wrong current password
    Given I am a user
    And I go to profile password page
    When I fill in password form    wrongPass    passChanged    passChanged
    And I save
    Then Page Should Contain    This value should be the user's current password
    And I log out
    And I am logging as    user

New passwords do not match
    Given I am a user
    And I go to profile password page
    When I fill in password form    user    passChanged    donotmatch
    And I save
    Then Page Should Contain    The entered passwords don't match
    And I log out
    And I am logging as    user

Impossible to change too short password
    Given I am a user
    And I go to profile password page
    When I fill in password form    user    passw    passw
    And I save
    Then Page Should Contain    This value is too short. It should have 6 characters or more.
    And I log out
    And I am logging as    user

Breadcrumb
    Given I am a user
    When I go to profile password page
    the breadcrumb should contain    Admin    Profile    Change password

Main tab
    Given I am a user
    And I go to profile password page
    When Click Link    Main
    Then Location Should Be    ${URL}/user/profile

Roles tab
    Given I am a user
    And I go to profile password page
    When Click Link    Roles
    Then Location Should Be    ${URL}/user/role

*** Keywords ***
I go to profile password page
    Click element    link=Admin
    Click element    link=Profile
    Click element    link=Password

I fill in password form
    [Arguments]    ${current}    ${new}    ${new1}
    Input Text    map_userbundle_usertype_current_password    ${current}
    Input Text    map_userbundle_usertype_plainPassword_first    ${new}
    Input Text    map_userbundle_usertype_plainPassword_second    ${new1}
