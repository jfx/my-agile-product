*** Settings ***
Documentation     In order to manage user
...               As a super-admin user profile
...               I need to delete a user.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Delete a user
    Given I am a super-admin
    And I go to users list
    Then Page Should Contain    lock@example.com
    And I click action button    Delete user #3
    When I click button    Remove
    And I confirm
    Then Location Should Be    ${URL}/user/
    Then Page Should Contain    User removed successfully
    Then Page Should not Contain    lock@example.com

Cancel to delete a user
    Given I am a super-admin
    And I go to users list
    Then Page Should Contain    lock@example.com
    And I click action button    Delete user #3
    When I click button    Remove
    And I do not confirm
    Then Location Should Be    ${URL}/user/del/3

Impossible to delete a user
    Given I am a super-admin
    And I go to users list
    And I click action button    Delete user #2
    When I click button    Remove
    And I confirm
    Then Location Should Be    ${URL}/user/del/2
    Then Page Should Contain    Impossible to remove this item - Integrity constraint violation !

Wrong user Id
    Given I am a super-admin
    And I go to users list
    When Go to    ${URL}/user/del/999
    Then I should see 404 error page

Breadcrumb
    Given I am a super-admin
    And I go to users list
    When I click action button    Delete user #3
    Then the breadcrumb should contain    Admin    Delete user

Return to list button
    Given I am a super-admin
    And I go to users list
    And I click action button    Delete user #3
    When I click button    Return to list
    Then Location Should Be    ${URL}/user/
