*** Settings ***
Documentation     In order to see details about a specific product
...               As a connected user
...               I need to view product details.
Resource          ../Keyword_resource.txt

*** Test Cases ***
View product details
    Given I am a user
    And I go to view product 1 page
    And I check product view form    Product One

Check context for product view with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/product/1
    Then the breadcrumb should contain    Product One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    ROLE_DM_USER    Update user

Check context for product view with previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/2
    And the breadcrumb should contain    Product Two    View
    And Page should contain    Useruser (User+)
    And I reset the context log file
    When Go To    ${URL_DEV}/product/1
    Then the breadcrumb should contain    Product One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    ROLE_DM_USER    Update user

Check context for product view with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And the breadcrumb should contain    Product One    View
    And Page should contain    Useruser (User)
    And I reset the context log file
    When Go To    ${URL_DEV}/product/1
    Then the breadcrumb should contain    Product One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Wrong product Id
    Given I am a user
    And I go to products list
    When Go to    ${URL}/product/999
    Then I should see 404 error page

Breadcrumb
    Given I am a user
    When I go to view product 1 page
    Then the breadcrumb should contain    Product One    View

Breadcrumb product link
    Given I am a super-admin
    When I go to view product 1 page
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Tab Main
    Given I am a user
    And I go to view product 1 page
    When Click Link    id=a_main
    And Location Should Be    ${URL}/product/1

Tab Releases
    Given I am a user
    And I go to view product 1 page
    And Element Should Contain    id=badge_releases    3
    When Click Link    id=a_releases
    Then Location Should Be    ${URL}/pdt-release/

Tab Users
    Given I am a user
    And I go to view product 1 page
    And Element Should Contain    id=badge_users    5
    When Click Link    id=a_users
    Then Location Should Be    ${URL}/pdt-user/

Return to list button
    Given I am a user
    And I go to view product 1 page
    When I click button    Return to list
    Then Location Should Be    ${URL}/product/

Edit button for super-admin profile
    Given I am a super-admin
    And I go to view product 1 page
    When I click button    Edit
    Then Location Should Be    ${URL}/product/edit/1

Edit button not displayed for non super-admin profile
    Given I am a user
    And I go to view product 1 page
    Then I should not see action button(s)    Edit

*** Keywords ***
I go to view product 1 page
    And I go to products list
    And I click action button    View product #1
