*** Settings ***
Documentation     In order to manage product
...               As a super-admin user profile
...               I need to add a product.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Add a product
    Given I am a super-admin
    And I go to products list
    And I click action button    Add
    When I fill in product form    Px
    And I save
    Then Page Should Contain    Product added successfully
    And I check product view form    Px
    And I click button    Return to list
    And the table should contain x rows    6
    And the table should contain    Px    Details 4 Px

Check context for product add with clean context
    [Tags]    context
    Given I am logging as    admin    admin    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/product/add
    Then the breadcrumb should contain    Admin    Add product
    And Page should contain    Useradmin*
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->unsetCurrentProduct    Reset childs : Release and above    User->unsetCurrentProduct    Update user

Check context for product add with previous context
    [Tags]    context
    Given I am logging as    admin    admin    Hello    True
    And Go To    ${URL_DEV}/product/1
    And I reset the context log file
    When Go To    ${URL_DEV}/product/add
    Then the breadcrumb should contain    Admin    Add product
    And Page should contain    Useradmin*
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->unsetCurrentProduct    Reset childs : Release and above    User->unsetCurrentProduct    Update user

Required and optional form labels
    Given I am a super-admin
    And I go to products list
    When I click action button    Add
    Then the label of this field is displayed as required    Name
    And the label of this field is displayed as optional    Details

Impossible to add a product with an empty name
    Given I am a super-admin
    And I go to products list
    And I click action button    Add
    When Input Text    map3_product_name    ${EMPTY}
    And I save
    Then Page Should Contain    This value should not be blank.

Impossible to add a product with a name too short
    Given I am a super-admin
    And I go to products list
    And I click action button    Add
    When Input Text    map3_product_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to add a product with a duplicate name
    Given I am a super-admin
    And I go to products list
    And I click action button    Add
    When Input Text    map3_product_name    Product One
    And I save
    Then Page Should Contain    A product with this name already exists.

Breadcrumb
    Given I am a super-admin
    And I go to products list
    When I click action button    Add
    Then the breadcrumb should contain    Admin    Add product

Return to list button
    Given I am a super-admin
    And I go to products list
    And I click action button    Add
    When I click button    Return to list
    Then Location Should Be    ${URL}/product/

*** Keywords ***
