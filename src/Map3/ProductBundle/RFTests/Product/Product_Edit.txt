*** Settings ***
Documentation     In order to manage product
...               As a super-admin user profile
...               I need to edit a product.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A super-admin edits a product
    Given I am a super-admin
    And I go to edit product 1 page
    When I fill in product form    P1Mod
    And I save
    Then Page Should Contain    Product edited successfully
    And I check product view form    P1Mod
    And I click button    Return to list
    And the table should contain x rows    5
    And the table should contain    P1Mod    Details 4 P1Mod

A manager edits a product
    Given I am a manager
    And I go to products list
    And I click action button    View product #1
    And I click button    Edit
    When I fill in product form    P1Mod
    And I save
    Then Page Should Contain    Product edited successfully
    And I check product view form    P1Mod
    And I click button    Return to list
    And the table should contain x rows    5
    And the table should contain    P1Mod    Details 4 P1Mod

Check context for product edit with clean context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/product/edit/1
    Then the breadcrumb should contain    Product One    Edit
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    ROLE_DM_MANAGER    Update user

Check context for product edit with previous context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/product/2
    And the breadcrumb should contain    Product Two    View
    And Page should contain    Userd1-manager
    And I reset the context log file
    When Go To    ${URL_DEV}/product/edit/1
    Then the breadcrumb should contain    Product One    Edit
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    ROLE_DM_MANAGER    Update user

Check context for product edit with same previous context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/product/1
    And the breadcrumb should contain    Product One    View
    And Page should contain    Userd1-manager (Manager)
    And I reset the context log file
    When Go To    ${URL_DEV}/product/edit/1
    Then the breadcrumb should contain    Product One    Edit
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Impossible to edit a product with an empty name
    Given I am a super-admin
    And I go to products list
    And I click action button    Edit product #1
    When Input Text    map3_product_name    ${EMPTY}
    And I save
    Then Page Should Contain    This value should not be blank.

Impossible to edit a product with a name too short
    Given I am a super-admin
    And I go to products list
    And I click action button    Edit product #1
    When Input Text    map3_product_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to edit a product with a duplicate name
    Given I am a super-admin
    And I go to edit product 1 page
    When Input Text    map3_product_name    Product Two
    And I save
    Then Page Should Contain    A product with this name already exists.

Wrong product Id
    Given I am a super-admin
    And I go to products list
    When Go to    ${URL}/product/edit/999
    Then I should see 404 error page

Breadcrumb
    Given I am a super-admin
    When I go to edit product 1 page
    Then the breadcrumb should contain    Product One    Edit

Breadcrumb product link
    Given I am a super-admin
    When I go to edit product 1 page
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Tab Main
    Given I am a super-admin
    And I go to edit product 1 page
    When Click Link    id=a_main
    And Location Should Be    ${URL}/product/1

Tab Releases
    Given I am a super-admin
    And I go to edit product 1 page
    And Element Should Contain    id=badge_releases    3
    When Click Link    id=a_releases
    Then Location Should Be    ${URL}/releases/1

Tab Users
    Given I am a super-admin
    And I go to edit product 1 page
    And Element Should Contain    id=badge_users    5
    When Click Link    id=a_users
    Then Location Should Be    ${URL}/pdt-user/

Return to list button
    Given I am a super-admin
    And I go to edit product 1 page
    When I click button    Return to list
    Then Location Should Be    ${URL}/product/

*** Keywords ***
I go to edit product 1 page
    And I go to products list
    And I click action button    Edit product #1
