*** Settings ***
Documentation     In order to manage a release
...               As a user with a manager role
...               I need to add a release.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A manager adds a release
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When I fill in release form    R1x
    And I save
    Then Page Should Contain    Release added successfully
    And I check release view form    R1x
    And I click button    Return to product
    And the table should contain x rows    4
    And the table should contain    R1x    Details 4 R1x

A manager adds a closed release
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When I fill in release form    R1x    3    True
    And I save
    Then Page Should Contain    Release added successfully
    And I check release view form    R1x    3    True
    And I click button    Return to product
    And the table should contain x rows    4
    And the table should contain    R1x    Details 4 R1x

Check context for release add with clean context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/product/1
    And I reset the context log file
    When Go To    ${URL_DEV}/release/add/1
    Then the breadcrumb should contain    Product One    Add release
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Check context for release add without context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/release/add/1
    Then the breadcrumb should contain    Product One    Add release
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    Role : ROLE_DM_MANAGER    Update user

Check context for release add with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And I reset the context log file
    When Go To    ${URL_DEV}/release/add/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for release add with same previous context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/release/add/1
    And I reset the context log file
    When Go To    ${URL_DEV}/release/add/1
    Then the breadcrumb should contain    Product One    Add release
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Required and optional form labels
    Given I am a manager
    And I go to releases list    View product #1
    When I click action button    Add
    Then the label of this field is displayed as required    Name
    And the label of this field is displayed as required    Release date
    And the label of this field is displayed as optional    Closed
    And the label of this field is displayed as optional    Details

Impossible to add a release with an empty name
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When I fill in release form    ${EMPTY}
    And I save
    Then Page Should Contain    This value should not be blank.

Impossible to add a release with a name too short
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When I fill in release form    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to add a release with a wrong date
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When Input text    map3_release_name    R1x
    And Input Text    //div[@data-link-field='map3_release_releaseDate']/input[@type='text']    xx/xx/xxxx
    And Click Element    map3_release_name
    And I save
    Then Page Should Contain    This value is not a valid date

Impossible to add a release with an empty date
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When Input text    map3_release_name    R1x
    And Input Text    //div[@data-link-field='map3_release_releaseDate']/input[@type='text']    ${EMPTY}
    And Click Element    map3_release_name
    And I save
    Then Page Should Contain    This value is not a valid date

Add a release without selecting a product before
    Given I am a manager
    When Go to    ${URL}/release/add/1
    Then Location should be    ${URL}/release/add/1
    And the breadcrumb should contain    Product One    Add release
    Then Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a manager
    And I go to releases list    View product #1
    When I click action button    Add
    Then the breadcrumb should contain    Product One    Add release

Breadcrumb product link
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Tab Main
    Given I am a manager
    And I go to releases list    View product #1
    When I click action button    Add
    When Click Link    id=a_main
    And Location Should Be    ${URL}/product/1

Tab Releases
    Given I am a manager
    And I go to releases list    View product #1
    When I click action button    Add
    And Element Should Contain    id=badge_releases    3
    When Click element    id=a_releases
    And Location Should Be    ${URL}/releases/1

Tab Users
    Given I am a manager
    And I go to releases list    View product #1
    When I click action button    Add
    And Element Should Contain    id=badge_users    5
    When Click Link    id=a_users
    Then Location Should Be    ${URL}/pdt-user/

Return to product button
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Add
    When I click button    Return to product
    Then Location Should Be    ${URL}/releases/1
