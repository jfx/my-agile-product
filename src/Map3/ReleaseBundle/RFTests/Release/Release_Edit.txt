*** Settings ***
Documentation     In order to manage a release
...               As a user with a manager role
...               I need to edit a release.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A manager edits a release
    Given I am a manager
    And I go to edit release #1 page
    When I fill in release form    R1Mod    0
    And I save
    Then Page Should Contain    Release edited successfully
    And I check release view form    R1Mod    1
    And I click button    Return to product
    Element Should Contain    xpath=//option    R1Mod
    And the table should contain x rows    3
    And the table should contain    R1Mod    Details 4 R1Mod

A manager closes a release
    Given I am a manager
    And I go to edit release #1 page
    When Select Checkbox    map3_release_closed
    And I save
    Then Page Should Contain    Release edited successfully
    And Checkbox Should Be Selected    map3_release_closed

A manager opens a closed release
    Given I am a manager
    And I go to releases list    View product #1
    And I click action button    Edit release #3
    When Unselect Checkbox    map3_release_closed
    And I save
    Then Page Should Contain    Release edited successfully
    And Checkbox Should Not Be Selected    map3_release_closed

Impossible to edit a release with a name too short
    Given I am a manager
    And I go to edit release #1 page
    When Input Text    map3_release_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to modify a release with wrong date
    Given I am a manager
    And I go to edit release #1 page
    When Input Text    //div[@data-link-field='map3_release_releaseDate']/input[@type='text']    xx/xx/xxxx
    And Click Element    map3_release_name
    And I save
    ${d}    Get Time    day
    Then Disabled text element should contain    map3_release_releaseDate    2014-09-${d}

Wrong release Id for a product
    Given I am a manager
    And I go to releases list    View product #1
    When Go to    ${URL}/release/edit/4
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Wrong release Id
    Given I am a manager
    And I go to releases list    View product #1
    When Go to    ${URL}/release/edit/999
    Then I should see 404 error page

Edit a release without selecting a product before
    Given I am a manager
    When Go to    ${URL}/release/edit/1
    Then Location should be    ${URL}/release/edit/1
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a manager
    When I go to edit release #1 page
    Then the breadcrumb should contain    Product One    Release One    Edit

Breadcrumb product link
    Given I am a manager
    And I go to edit release #1 page
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a manager
    And I go to edit release #1 page
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Tab Baselines
    Given I am a manager
    And I go to edit release #1 page
    When Click Link    Baselines
    Then Location Should Be    ${URL}/release/edit/1#

Return to product button
    Given I am a manager
    And I go to edit release #1 page
    When I click button    Return to product
    Then Location Should Be    ${URL}/pdt-release/

*** Keywords ***
I go to edit release #1 page
    And I go to releases list    View product #1
    And I click action button    Edit release #1
