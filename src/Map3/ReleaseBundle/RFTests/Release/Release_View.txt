*** Settings ***
Documentation     In order to see details about a specific release
...               As a connected user
...               I need to view release details.
Resource          ../Keyword_resource.txt

*** Test Cases ***
View release details
    Given I am a user
    And I go to releases list    View product #1
    When I click action button    View release #3
    Then Location Should Be    ${URL}/release/3
    And Disabled text element should contain    map3_release_name    Release Closed
    And Disabled text element should contain    map3_release_releaseDate    2014-08-14
    And Checkbox Should Be Selected    map3_release_closed

Check context for release view with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And I reset the context log file
    When Go To    ${URL_DEV}/release/1
    Then the breadcrumb should contain    Product One    Release One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    Update user    Same product. No change

Check context for release view with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And I reset the context log file
    When Go To    ${URL_DEV}/release/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    Update user    User->setCurrentProduct    Role : ROLE_DM_NONE

Check context for release view with same previous product
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/release/1
    Then the breadcrumb should contain    Product One    Release One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    Update user    Same product. No change

Check context for release view with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And I reset the context log file
    When Go To    ${URL_DEV}/release/1
    Then the breadcrumb should contain    Product One    Release One    View
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    Same release. No change    Update user

Wrong release Id for a product
    Given I am a manager
    And I go to releases list    View product #1
    When Go to    ${URL}/release/4
    Then Location Should Be    ${URL}/release/4
    And Disabled text element should contain    map3_release_name    Release Three

Wrong release Id
    Given I am a user
    And I go to releases list    View product #1
    When Go to    ${URL}/release/999
    Then I should see 404 error page

Impossible to view a release without selecting a product before
    Given I am a user
    When Go to    ${URL}/release/1
    Then Location should be    ${URL}/release/1
    And Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Edit button for a manager role on product
    Given I am a manager
    And I go to view release #1 page
    When I click button    Edit
    Then Location Should Be    ${URL}/release/edit/1

Edit button not displayed for a non manager role
    Given I am a user
    And I go to view release #1 page
    Then I should not see action button(s)    Edit

Breadcrumb
    Given I am a user
    When I go to view release #1 page
    Then the breadcrumb should contain    Product One    Release One    View

Breadcrumb product link
    Given I am a user
    And I go to view release #1 page
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user
    And I go to view release #1 page
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Tab Main
    Given I am a user
    And I go to view release #1 page
    When Click Link    id=a_main
    And Location Should Be    ${URL}/release/1

Tab Baselines
    Given I am a manager
    And I go to view release #1 page
    And Element Should Contain    id=badge_baselines    3
    When Click element    id=a_baselines
    And Location Should Be    ${URL}/rls-baseline/

Return to product button
    Given I am a user
    And I go to view release #1 page
    When I click button    Return to product
    Then Location Should Be    ${URL}/pdt-release/

Edit button displayed for manager profile
    Given I am a manager
    And I go to view release #1 page
    When I click button    Edit
    Then Location Should Be    ${URL}/release/edit/1

Edit button not displayed for non manager profile
    Given I am a user
    And I go to view release #1 page
    Then I should not see action button(s)    Edit

*** Keywords ***
I go to view release #1 page
    And I go to releases list    View product #1
    And I click action button    View release #1
