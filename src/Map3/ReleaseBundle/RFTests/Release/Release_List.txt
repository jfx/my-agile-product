*** Settings ***
Documentation     In order to manage releases for a product
...               As a connected user
...               I need to see releases list.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Display releases list on a product for a user
    Given I am a user
    When I go to releases list    View product #1
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    3
    And the table should contain    Release Closed    14/08/2014    Details 4 release closed
    And the table should contain    Release One
    And the table should contain    Release Two
    And Element Should Contain    id=badge_releases    3

List with no release on a product for a user
    Given I am a user
    When I go to releases list    View product #5
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    1
    And the table should contain    No release
    And Element Should Contain    id=badge_releases    0

Check context for releases list with clean context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/product/1
    And I reset the context log file
    When Go To    ${URL_DEV}/releases/1
    Then the breadcrumb should contain    Product One    Releases list
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Check context for releases list without context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/releases/1
    Then the breadcrumb should contain    Product One    Releases list
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    Role : ROLE_DM_MANAGER    Update user

Check context for releases list with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And I reset the context log file
    When Go To    ${URL_DEV}/releases/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for releases list with same previous context
    [Tags]    context
    Given I am logging as    d1-manager    d1-manager    Hello    True
    And Go To    ${URL_DEV}/releases/1
    And I reset the context log file
    When Go To    ${URL_DEV}/releases/1
    Then the breadcrumb should contain    Product One    Releases list
    And Page should contain    Userd1-manager (Manager)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentProduct    Reset childs : Release and above    Same product. No change    Update user

Display releases list without selecting a product before
    Given I am a user
    When Go to    ${URL}/releases/1
    Then Location should be    ${URL}/releases/1
    And the breadcrumb should contain    Product One    Releases list
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

No modify actions buttons for a non-manager
    Given I am a user
    When I go to releases list    View product #1
    Then the table should contain x rows    3
    And I should see action button(s)    View release #1
    And I should not see action button(s)    Add    Edit release #1    Delete release #1

Action buttons Add/View/Edit/Delete for a manager
    Given I am a manager
    When I go to releases list    View product #1
    Then the table should contain x rows    3
    And I should see action button(s)    Add    View release #1    Edit release #1    Delete release #1

Breadcrumb
    Given I am a user
    When I go to releases list    View product #1
    Then the breadcrumb should contain    Product One    Releases list

Breadcrumb product link
    Given I am a user
    And I go to releases list    View product #1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Tab Main
    Given I am a user
    And I go to releases list    View product #1
    When Click Link    id=a_main
    And Location Should Be    ${URL}/product/1

Tab Releases
    Given I am a manager
    And I go to releases list    View product #1
    And Element Should Contain    id=badge_releases    3
    When Click element    id=a_releases
    And Location Should Be    ${URL}/releases/1

Tab Users
    Given I am a user
    And I go to releases list    View product #1
    And Element Should Contain    id=badge_users    5
    When Click Link    id=a_users
    Then Location Should Be    ${URL}/pdt-user/
