*** Settings ***
Documentation     In order to manage releases for a product
...               As a connected user
...               I need to see releases list.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Display releases list on a product for a user
    Given I am a user
    When I go to releases list    View product #1
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    3
    And the table should contain    Release Closed    31/07/2013    Details 4 release closed
    And the table should contain    Release One
    And the table should contain    Release Two
    And Element Should Contain    id=badge_releases    3

List with no release on a product for a user
    Given I am a user
    When I go to releases list    View product #3
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    1
    And the table should contain    No release
    And Element Should Contain    id=badge_releases    0

Impossible to view releases list without selecting a product before
    Given I am a user
    When Go to    ${URL}/pdt-release/
    Then Location Should Be    ${URL}/product/

No modify actions buttons for a non-manager
    Given I am a user
    When I go to releases list    View product #1
    Then the table should contain x rows    3
    And I should see action button(s)    View release #1
    And I should not see action button(s)    Add    Edit release #1    Delete release #1

Action buttons Add/View/Edit/Delete for a manager
    Given I am a manager
    When I go to releases list    View product #1
    Then the table should contain x rows    3
    And I should see action button(s)    Add    View release #1    Edit release #1    Delete release #1

Breadcrumb
    Given I am a user
    When I go to releases list    View product #1
    Then the breadcrumb should contain    Product One    Releases list

Breadcrumb product link
    Given I am a user
    And I go to releases list    View product #1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Tab Main
    Given I am a user
    And I go to releases list    View product #1
    When Click Link    id=a_main
    And Location Should Be    ${URL}/product/1

Tab Users
    Given I am a user
    And I go to releases list    View product #1
    And Element Should Contain    id=badge_users    5
    When Click Link    id=a_users
    Then Location Should Be    ${URL}/pdt-user/
