*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ deletes a feature
    [Tags]    db
    [Setup]    TestSetupDb
    Given I am a user+
    And Check If Exists In Database    select id from map3_feature where id = 3
    And Check If Exists In Database    select id from map3_feature where id = 4 and position = 3
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_3_anchor
    And I Click button    Delete node
    And I wait until text is visible    Delete feature
    When I click button    Remove
    And I confirm
    Then I wait until text is visible    Feature removed successfully
    And Check If Not Exists In Database    select id from map3_feature where id = 3
    And Check If Exists In Database    select id from map3_feature where id = 4 and position = 2
    [Teardown]    TestTeardownDb

A user+ deletes a feature with children
    [Tags]    db
    [Setup]    TestSetupDb
    Given I am a user+
    And Check If Exists In Database    select id from map3_feature where id = 1
    And Check If Exists In Database    select id from map3_feature where id = 2 and position = 1
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_1_anchor
    And I Click button    Delete node
    And I wait until text is visible    Delete feature
    When I click button    Remove
    And I confirm
    Then I wait until text is visible    Feature removed successfully
    And Check If Not Exists In Database    select id from map3_feature where id = 1
    And Check If Exists In Database    select id from map3_feature where id = 2 and position = 0
    [Teardown]    TestTeardownDb

Cancel to delete a feature
    Given I am a user+
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node to display sub-node    C_9
    And I click node    F_3_anchor
    And I Click button    Delete node
    And I wait until text is visible    Delete feature
    When I click button    Remove
    And I do not confirm
    And I am waiting for ajax request
    Then Page Should Not Contain    Feature removed successfully
    And Page Should Contain    Feature C3

Reload warning after timeout when click Delete button
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node    F_6_anchor
    When I am waiting for timeout
    And I Click button    Delete node
    Then I wait until text is visible    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Reload warning after timeout when click Remove
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node    F_6_anchor
    And I Click button    Delete node
    When I am waiting for timeout
    And I click button    Remove
    Then I wait until text is visible    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Reload warning after timeout when confirm
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node to display sub-node    B_1
    And I click node    F_6_anchor
    And I Click button    Delete node
    And I wait until text is visible    Delete feature
    And I click button    Remove
    When I am waiting for timeout
    And I confirm
    Then I wait until text is visible    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request delete feature
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Delete feature
    [Teardown]    EnvPostrequisite

Impossible to delete a feature for a closed baseline
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-ftree/3/del/F_7
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    Forbidden
    [Teardown]    EnvPostrequisite

Request delete feature with non numeric Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_XXX
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong node Id
    [Teardown]    EnvPostrequisite

Request delete feature with wrong node Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request delete feature with wrong baseline Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-ftree/999/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Feature C1    Narrative F1
    [Teardown]    EnvPostrequisite

Request delete feature timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for feature ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/del/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite
