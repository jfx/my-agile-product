*** Settings ***
Documentation     In order to manage features for a baseline
...               As a connected user
...               I need to see the features tree.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Display the features tree for a baseline for a user
    Given I am a user
    When I go to features tree    View product #1    View release #1    View baseline #1
    Then the node name should be equal to    B_1_anchor    Baseline One
    And Element Should Contain    id=badge_features    XX

Tree with no feature for a baseline
    Given I am a user
    When I go to features tree    View product #1    View release #3    View baseline #8
    Then the node name should be equal to    B_8_anchor    Baseline Closed 4 release closed
    And Element Should Contain    id=badge_features    XX

View features tree without selecting a baseline before
    Given I am a user
    When Go to    ${URL}/bln-features/1
    Then Location Should Be    ${URL}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And the breadcrumb should contain    Product One    Release One    Baseline One    Features tree

Impossible to delete a baseline
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And Click Element    B_1_anchor
    And I Click button    Delete node
    And I am waiting for ajax request
    Then Page Should Contain    Operation not allowed

Request root baseline node
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/child?pid=%23
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    B_1
    And the element should contain items    ${resp.content}    Baseline One
    [Teardown]    EnvPostrequisite

Reload warning after timeout when click to display sub-categories
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I am waiting for timeout
    When I click node to display sub-node    B_1
    And I am waiting for ajax request
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Re-authentication after reload warning
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I am waiting for timeout
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    And Page should contain    Authentication has expired
    When Click Link    id=reload
    And Location Should Be    ${URL_DEV}/login
    And I fill in login form    useruser    user
    And Click element    xpath=//button[@title='Login']
    Then Location Should Be    ${URL_DEV}/bln-features/1
    [Teardown]    TestLifetimeTeardown

Cookie saves the tree state
    Given I am a user
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    And I click node to display sub-node    C_9
    And I am waiting for ajax request
    And I click node to display sub-node    C_12
    And I am waiting for ajax request
    And Element Should Be Visible    C_19_anchor
    And Element Should Not Be Visible    C_15_anchor
    And Click element    link=Log out
    And Location Should Be    ${URL_DEV}/login
    And I am a user
    When Go To    ${URL_DEV}/bln-features/1
    And I am waiting for ajax request    3
    Then Element Should Be Visible    C_19_anchor
    And Element Should Not Be Visible    C_15_anchor

Check context for features tree with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree
    And Page should contain    Useruser (User)
    And I am waiting for ajax request
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Check context for features tree without context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree
    And Page should contain    Useruser (User)
    And I am waiting for ajax request
    And the context log file should contain x lines    11
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user

Check context for features tree with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And Go To    ${URL_DEV}/baseline/6
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And I am waiting for ajax request
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for features tree with same previous product
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree
    And Page should contain    Useruser (User)
    And I am waiting for ajax request
    And the context log file should contain x lines    10
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user

Check context for features tree with same previous release
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree
    And Page should contain    Useruser (User)
    And I am waiting for ajax request
    And the context log file should contain x lines    9
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for features tree with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-features/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree
    And Page should contain    Useruser (User)
    And I am waiting for ajax request
    And the context log file should contain x lines    10
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Breadcrumb
    Given I am a user
    When I go to features tree    View product #1    View release #1    View baseline #1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Features tree

Breadcrumb product link
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    When Click Link    id=a_main
    Then Location Should Be    ${URL}/baseline/1

Tab Features
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And Element Should Contain    id=badge_features    XX
    When Click element    id=a_features
    Then Location Should Be    ${URL}/bln-features/1

Tab References
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And Element Should Contain    id=badge_references    3
    When Click element    id=a_references
    Then Location Should Be    ${URL}/bln-refs/1

Refresh button
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I am waiting for ajax request
    And Click element    xpath=//button[@title='Expand all']
    And I am waiting for ajax request    3
    When Click element    xpath=//button[@title='Refresh']
    And Page should not contain    Category 111
    And I am waiting for ajax request    3
    Then Page should contain    Category 111

Expand all button
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I am waiting for ajax request
    And Page should not contain    Category 111
    When Click element    xpath=//button[@title='Expand all']
    And I am waiting for ajax request    3
    Then Page should contain    Category 111

Collapse all button
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I am waiting for ajax request
    And Click element    xpath=//button[@title='Expand all']
    And I am waiting for ajax request    3
    And Page should contain    Category 111
    When Click element    xpath=//button[@title='Collapse all']
    Then Page should not contain    Category 111

No Add button for a closed baseline
    Given I am a user+
    When I go to features tree    View product #1    View release #1    View baseline #3
    Then Page Should Not Contain Element    addMenu

No Add button for a guest user
    Given I am a guest
    When I go to features tree    View product #1    View release #1    View baseline #1
    Then Page Should Not Contain Element    addMenu

No Delete button for a closed baseline
    Given I am a user+
    When I go to features tree    View product #1    View release #1    View baseline #3
    Then I should not see action button(s)    Delete node

No Delete button for a guest user
    Given I am a guest
    When I go to features tree    View product #1    View release #1    View baseline #1
    Then I should not see action button(s)    Delete node
