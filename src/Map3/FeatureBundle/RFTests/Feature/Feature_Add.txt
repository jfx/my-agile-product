*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ adds a feature from baseline
    [Setup]    TestSetupDb
    Given I am a user+
    And Check If Not Exists In Database    select id from map3_feature where category_id = 1 and position = 1
    And I go to features tree    View product #1    View release #1    View baseline #1
    And I click node    B_1_anchor
    When Click Element    addMenu
    And I Click button    Add feature
    When I fill in feature form    Feature added    ID-001-Added    P0 - Must have    Narrative Added
    And I click button    Save
    Then I wait until text is visible    Feature added successfully
    And I check feature view form    Feature added    ID-001-Added    P0 - Must have    Narrative Added
    And Check If Exists In Database    select id from map3_feature where category_id = 1 and position = 1
    [Teardown]    TestTeardownDb

A user+ adds a feature from category with default value
    Given I am a user+
    And I go directly to features tree    1
    And I click node to display sub-node    B_1
    And I click node    C_9_anchor
    And Click Element    addMenu
    And I Click button    Add feature
    When Wait Until Element Is Visible    map3_feature_title    ${WAIT_TIMEOUT}
    And Input text    map3_feature_title    Feature added
    And I click button    Save
    Then I wait until text is visible    Feature added successfully
    And I check feature view form    Feature added    ${EMPTY}    P1 - Should have    In order to [Benefit]\nAs a [Role]\nI want [Feature]

Reload warning after timeout when click Add feature
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node    B_1_anchor
    When Click Element    addMenu
    And I am waiting for timeout
    And I Click button    Add feature
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Reload warning after timeout when click Save
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And I click node    B_1_anchor
    And Click Element    addMenu
    And I Click button    Add feature
    When I fill in feature form    Feature added    ID-001-Added    P0 - Must have    Narrative Added
    And I am waiting for timeout
    And I click button    Save
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request add feature from baseline
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/B_1
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Add feature
    [Teardown]    EnvPostrequisite

Request add feature from category
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Add feature
    [Teardown]    EnvPostrequisite

Impossible to add a feature for a closed baseline
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_25
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the element should contain items    ${resp.content}    You are not allowed to access this resource
    [Teardown]    EnvPostrequisite

Request add feature with non existent parent baseline Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/B_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request add feature with non existent type of parent node
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/X_1
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong type of node
    [Teardown]    EnvPostrequisite

Request add feature with multiple selected nodes
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/B_1,C_9
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Please, select only one node !
    [Teardown]    EnvPostrequisite

Request add feature with not allowed parent node - feature
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/F_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add feature with not allowed parent node - scenario
    [Tags]    request    failedTest
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-feat/add/S_1
    Then Should Be Equal As Strings    ${resp.status_code}    405
    And Should Contain    ${resp.content}    Operation not allowed
    [Teardown]    EnvPostrequisite

Request add feature timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for feature ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for feature ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-feat/add/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Impossible to edit a feature with an empty title
    Given I am a user+
    And I go directly to features tree    1
    And I click node    B_1_anchor
    And Click Element    addMenu
    And I Click button    Add feature
    When I fill in feature form    ${EMPTY}    ID-001-Added    P0 - Must have    Narrative Added
    And I click button    Save
    Then I wait until text is visible    This value should not be blank.

Impossible to add a feature with a title too short
    Given I am a user+
    And I go directly to features tree    1
    And I click node    B_1_anchor
    And Click Element    addMenu
    And I Click button    Add feature
    When I fill in feature form    X    ID-001-Added    P0 - Must have    Narrative Added
    And I click button    Save
    Then I wait until text is visible    This value is too short. It should have 2 characters or more.

No Add category menu for a user
    Given I am a user
    And I go directly to features tree    1
    And I should not see action button(s)    Add feature
