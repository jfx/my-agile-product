*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
Click to view a category node
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    Then I check category view form    Category 1

Reload warning after timeout when click to view category
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for timeout
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request view category
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Category 1    Details 4 Category 1
    [Teardown]    EnvPostrequisite

Request view category with non numeric Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_XXX
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Wrong node Id
    [Teardown]    EnvPostrequisite

Request view category with wrong node Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request view category with wrong baseline Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user
    ${resp}=    When Ajax Get Request    /bln-ftree/999/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Category 1    Details 4 Category 1
    [Teardown]    EnvPostrequisite

Request view category timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    user    user    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for category ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    user    user    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-ftree/1/node/C_9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Edit button displayed for user+ profile
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    When I click button    Edit
    And I am waiting for ajax request
    Then Location Should Be    ${URL}/bln-features/1
    And I should see action button(s)    Save

Reload warning after timeout when click edit button
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    And Click Element    C_9_anchor
    And I am waiting for timeout
    When I click button    Edit
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Edit button not displayed for closed baseline
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #3
    And the node name should be equal to    B_7_anchor    Baseline Closed
    And I click node to display sub-node    B_7
    And I am waiting for ajax request
    When Click Element    C_25_anchor
    And I am waiting for ajax request
    Then I should not see action button(s)    Edit

Edit button not displayed for non user+ profile
    Given I am a user
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    Then I should not see action button(s)    Edit
