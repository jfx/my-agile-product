*** Settings ***
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ edits a category
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    And I click button    Edit
    And I am waiting for ajax request
    When I fill in category form    Ctg 1 Mod
    And I click button    Save
    And I am waiting for ajax request
    Then Page Should Contain    Category edited successfully
    And I check category view form    Ctg 1 Mod

Reload warning after timeout when click to edit category
    [Tags]    timeout
    [Setup]    TestLifetimeSetup
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-features/1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    And I click button    Edit
    And I am waiting for timeout
    And I click button    Save
    Then Page should contain    Authentication has expired
    [Teardown]    TestLifetimeTeardown

Request edit category
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the element should contain items    ${resp.content}    Category 1    Details 4 Category 1
    [Teardown]    EnvPostrequisite

Request edit category with wrong node Id
    [Tags]    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+
    ${resp}=    When Ajax Get Request    /bln-cat/edit/999
    Then Should Be Equal As Strings    ${resp.status_code}    404
    And Should Contain    ${resp.content}    Not Found
    [Teardown]    EnvPostrequisite

Request edit category timeout
    [Tags]    request    timeout
    [Setup]    RequestLifetimeSetup
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I am waiting for timeout
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And Should Contain    ${resp.content}    A Token was not found in the SecurityContext
    [Teardown]    RequestLifetimeTeardown

Check context for category ajax request with clean context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request without context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-none    d1-none    Hello    True
    And Get request    ${SESSION}    /product/2
    And Get request    ${SESSION}    /release/4
    And Get request    ${SESSION}    /baseline/6
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    403
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous product
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous release
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /product/1
    And Get request    ${SESSION}    /release/1
    And Get request    ${SESSION}    /baseline/2
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    5
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user
    [Teardown]    EnvPostrequisite

Check context for category ajax request with same previous context
    [Tags]    context    request
    [Setup]    EnvPrerequisite
    Given I am logging as by request    d1-user+    d1-user+    Hello    True
    And Get request    ${SESSION}    /bln-features/1
    And I reset the context log file
    ${resp}=    When Ajax Get Request    /bln-cat/edit/9
    Then Should Be Equal As Strings    ${resp.status_code}    200
    And the context log file should contain x lines    3
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user
    [Teardown]    EnvPostrequisite

Impossible to edit a category with a name too short
    Given I am a user+
    And I go to features tree    View product #1    View release #1    View baseline #1
    And the node name should be equal to    B_1_anchor    Baseline One
    And I click node to display sub-node    B_1
    And I am waiting for ajax request
    When Click Element    C_9_anchor
    And I am waiting for ajax request
    And I click button    Edit
    And I am waiting for ajax request
    When Input text    map3_category_name    X
    And I click button    Save
    And I am waiting for ajax request
    Then Page Should Contain    This value is too short. It should have 2 characters or more.
