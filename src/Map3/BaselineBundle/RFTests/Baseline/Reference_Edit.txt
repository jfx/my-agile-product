*** Settings ***
Documentation     In order to manage a reference
...               As a user with a user+ role
...               I need to edit a reference.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ edits a reference
    Given I am a user+
    And I go to edit reference page    1
    When I fill in reference form    R1Mod
    When I save
    Then Location Should Be    ${URL}/bln-refs/1
    And Page Should Contain    Reference edited successfully
    And the table should contain x rows    3
    And the table should contain    R1Mod    Value 4 R1Mod

Check context for reference edit with clean context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Check context for reference edit without context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user

Check context for reference edit with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And Go To    ${URL_DEV}/baseline/5
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for reference edit with same previous product
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user

Check context for reference edit with same previous release
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for reference edit with same previous context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/bln-refs/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-ref/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Impossible to edit a reference with a name too short
    Given I am a user+
    And I go to edit reference page    1
    When I fill in reference form    X
    When I save
    Then Location Should Be    ${URL}/bln-ref/edit/1
    And Page Should Contain    This value is too short. It should have 2 characters or more.

Wrong reference Id
    Given I am a user+
    When I go to references list    View product #1    View release #1    View baseline #1
    When Go to    ${URL}/bln-ref/edit/999
    Then I should see 404 error page

Edit a reference without selecting a baseline before
    Given I am a user+
    When Go to    ${URL}/bln-ref/edit/1
    Then Location should be    ${URL}/bln-ref/edit/1
    And the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference

Breadcrumb
    Given I am a user+
    And I go to edit reference page    1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit reference

Breadcrumb product link
    Given I am a user+
    And I go to edit reference page    1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to edit reference page    1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user+
    And I go to edit reference page    1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user+
    And I go to edit reference page    1
    When Click Link    id=a_main
    Then Location Should Be    ${URL}/baseline/1

Tab References
    Given I am a user+
    And I go to edit reference page    1
    And Element Should Contain    id=badge_references    3
    When Click element    id=a_references
    Then Location Should Be    ${URL}/bln-refs/1

Tab Features
    Given I am a user+
    And I go to edit reference page    1
    And Element Should Contain    id=badge_features    XX
    When Click element    id=a_features
    Then Location Should Be    ${URL}/bln-ref/edit/1#

Return to list button
    Given I am a user+
    And I go to edit reference page    1
    When I click button    Return to list
    Then Location Should Be    ${URL}/bln-refs/1

*** Keywords ***
I go to edit reference page
    [Arguments]    ${id}
    I go to references list    View product #1    View release #1    View baseline #1
    I click action button    Edit reference #${id}
