*** Settings ***
Documentation     In order to manage references for a baseline
...               As a connected user
...               I need to see references list.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Display references list for a baseline for a user+
    Given I am a user+
    When I go to references list    View product #1    View release #1    View baseline #1
    Then the table header should contain    Action    Label    Value
    And the table should contain x rows    3
    And the table should contain    Reference One    Value 4 reference one
    And the table should contain    Reference Two    Value 4 reference two
    And the table should contain    Reference Three    Value 4 reference three
    And Element Should Contain    id=badge_references    3

List with no reference for a baseline for a user
    Given I am a user
    When I go to references list    View product #2    View release #4    View baseline #6
    Then the table header should contain    Label    Value
    And the table should contain x rows    1
    And the table should contain    No reference
    And Element Should Contain    id=badge_references    0

View baselines list without selecting a release before
    Given I am a user
    When Go to    ${URL}/bln-refs/1
    Then Location Should Be    ${URL}/bln-refs/1
    And the breadcrumb should contain    Product One    Release One    Baseline One    References list

Check context for reference list with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Check context for reference list without context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user

Check context for reference list with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And Go To    ${URL_DEV}/baseline/6
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for reference list with same previous product
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user

Check context for reference list with same previous release
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/2
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for reference list with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/bln-refs/1
    And I reset the context log file
    When Go To    ${URL_DEV}/bln-refs/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

No actions buttons for a closed baseline
    Given I am a user+
    When I go to references list    View product #1    View release #1    View baseline #3
    Then the table should contain x rows    1
    And I should not see action button(s)    Add    Edit reference #6    Delete reference #6

No actions buttons for a non-user+ role
    Given I am a user
    When I go to references list    View product #1    View release #1    View baseline #1
    Then the table should contain x rows    3
    And I should not see action button(s)    Add    Edit reference #1    Delete reference #1

Action buttons Add/View/Edit/Delete for a user+
    Given I am a user+
    When I go to references list    View product #1    View release #1    View baseline #1
    Then the table should contain x rows    3
    And I should see action button(s)    Add    Edit reference #1    Delete reference #1

Breadcrumb
    Given I am a user
    When I go to references list    View product #1    View release #1    View baseline #1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    References list

Breadcrumb product link
    Given I am a user
    And I go to references list    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to references list    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user+
    And I go to references list    View product #1    View release #1    View baseline #1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user
    And I go to references list    View product #1    View release #1    View baseline #1
    When Click Link    id=a_main
    Then Location Should Be    ${URL}/baseline/1

Tab Features
    Given I am a user
    And I go to references list    View product #1    View release #1    View baseline #1
    And Element Should Contain    id=badge_features    6
    When Click element    id=a_features
    Then Location Should Be    ${URL}/bln-features/1

Tab References
    Given I am a user
    And I go to references list    View product #1    View release #1    View baseline #1
    And Element Should Contain    id=badge_references    3
    When Click element    id=a_references
    Then Location Should Be    ${URL}/bln-refs/1
