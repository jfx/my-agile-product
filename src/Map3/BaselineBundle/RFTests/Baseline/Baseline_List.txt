*** Settings ***
Documentation     In order to manage baselines for a release
...               As a connected user
...               I need to see baselines list.
Resource          ../Keyword_resource.txt

*** Test Cases ***
Display baselines list on a release for a user
    Given I am a user
    When I go to baselines list    View product #1    View release #1
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    3
    And the table should contain    Baseline Closed    02/08/2014 12:00    Details 4 baseline closed
    And the table should contain    Baseline One
    And the table should contain    Baseline Two
    And Element Should Contain    id=badge_baselines    3

List with no baseline on a release for a user
    Given I am a user
    When I go to baselines list    View product #4    View release #6
    Then the table header should contain    Action    Name    Date    Closed    Details
    And the table should contain x rows    1
    And the table should contain    No baseline
    And Element Should Contain    id=badge_baselines    0

Check context for baselines list with clean context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    When Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then the breadcrumb should contain    Product One    Release One    Baselines list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    Same release. No change    Update user

Check context for baselines list without context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then the breadcrumb should contain    Product One    Release One    Baselines list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USER    Update user

Check context for baselines list with previous context
    [Tags]    context
    Given I am logging as    d1-none    d1-none    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_NONE    Update user

Check context for baselines list with same previous product
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then the breadcrumb should contain    Product One    Release One    Baselines list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    User->setCurrentRelease    Same product. No change    Update user

Check context for baselines list with same previous release
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then the breadcrumb should contain    Product One    Release One    Baselines list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    Same release. No change    Update user

Check context for baselines list with same previous context
    [Tags]    context
    Given I am logging as    user    user    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baselines/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baselines/1
    Then the breadcrumb should contain    Product One    Release One    Baselines list
    And Page should contain    Useruser (User)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentRelease    Reset childs : Baseline and above    Same release. No change    Update user

Display baselines list without selecting a product before
    Given I am a user
    When Go to    ${URL}/baselines/1
    Then Location should be    ${URL}/baselines/1
    And the breadcrumb should contain    Product One    Release One    Baselines list
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

No modify actions buttons for a closed release
    Given I am a user+
    When I go to baselines list    View product #1    View release #3
    Then the table should contain x rows    1
    And I should see action button(s)    View baseline #8
    And I should not see action button(s)    Add    Edit baseline #8    Delete baseline #8

No modify actions buttons for a non-user+ role
    Given I am a user
    When I go to baselines list    View product #1    View release #1
    Then the table should contain x rows    3
    And I should see action button(s)    View baseline #1
    And I should not see action button(s)    Add    Edit baseline #1    Delete baseline #1

Action buttons Add/View/Edit/Delete for a user+
    Given I am a user+
    When I go to baselines list    View product #1    View release #1
    Then the table should contain x rows    3
    And I should see action button(s)    Add    View baseline #1    Edit baseline #1    Delete baseline #1

Breadcrumb
    Given I am a user
    When I go to baselines list    View product #1    View release #1
    Then the breadcrumb should contain    Product One    Release One    Baselines list

Breadcrumb product link
    Given I am a user
    And I go to baselines list    View product #1    View release #1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Tab Main
    Given I am a user
    And I go to baselines list    View product #1    View release #1
    When Click Link    id=a_main
    Then Location Should Be    ${URL}/release/1

Tab Baselines
    Given I am a manager
    And I go to baselines list    View product #1    View release #1
    And Element Should Contain    id=badge_baselines    3
    When Click element    id=a_baselines
    Then Location Should Be    ${URL}/baselines/1
