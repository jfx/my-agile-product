*** Settings ***
Documentation     In order to manage a baseline
...               As a user with a user+ role
...               I need to edit a baseline.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ edits a baseline
    Given I am a user+
    And I go to edit baseline page    1
    When I fill in baseline form    B1Mod    1
    And I save
    Then Page Should Contain    Baseline edited successfully
    And I check baseline view form    B1Mod    1
    And I click button    Return to release
    And the table should contain x rows    3
    And the table should contain    B1Mod    Details 4 B1Mod

A manager closes a release
    Given I am a manager
    And I go to edit baseline page    1
    When Select Checkbox    map3_baseline_closed
    And I save
    Then Page Should Contain    Baseline edited successfully
    And Checkbox Should Be Selected    map3_baseline_closed

A manager opens a closed release
    Given I am a manager
    And I go to edit baseline page    3
    When Unselect Checkbox    map3_baseline_closed
    And I save
    Then Page Should Contain    Baseline edited successfully
    And Checkbox Should Not Be Selected    map3_baseline_closed

Impossible to edit a release with a name too short
    Given I am a manager
    And I go to edit baseline page    1
    When Input Text    map3_baseline_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to modify a release with wrong date
    Given I am a manager
    And I go to edit baseline page    3
    When Input Text    //div[@data-link-field='map3_baseline_baselineDatetime']/input[@type='text']    xx/xx/xxxx
    And Click Element    map3_baseline_name
    And I save
    ${d}    Get Time    day
    Then Disabled text element should contain    map3_baseline_baselineDatetime    2014-08-02 12:00

Wrong baseline Id for a release but same product
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/4
    Then Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}
    And Page Should Contain    Release Two

Wrong baseline Id for a release
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/5
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Wrong baseline Id
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/999
    Then I should see 404 error page

Edit a baseline without selecting a product before
    Given I am a user+
    When Go to    ${URL}/baseline/edit/1
    Then Location should be    ${URL}/baseline/edit/1
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a user+
    When I go to edit baseline page    1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit

Breadcrumb product link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=a_main
    And Location Should Be    ${URL}/baseline/1

Tab References
    [Tags]    failedTest
    Given I am a user+
    And I go to edit baseline page    1
    And Element Should Contain    id=badge_references    XX
    When Click element    id=a_references
    And Location Should Be    ${URL}/baseline/edit/1#

Tab Features
    [Tags]    failedTest
    Given I am a user+
    And I go to edit baseline page    1
    And Element Should Contain    id=badge_features    XX
    When Click element    id=a_features
    And Location Should Be    ${URL}/baseline/edit/1#

Return to release button
    Given I am a user+
    And I go to edit baseline page    1
    When I click button    Return to release
    Then Location Should Be    ${URL}/rls-baseline/

*** Keywords ***
I go to edit baseline page
    [Arguments]    ${id}
    I go to baselines list    View product #1    View release #1
    I click action button    Edit baseline #${id}
