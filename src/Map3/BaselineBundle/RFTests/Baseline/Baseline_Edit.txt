*** Settings ***
Documentation     In order to manage a baseline
...               As a user with a user+ role
...               I need to edit a baseline.
Resource          ../Keyword_resource.txt

*** Test Cases ***
A user+ edits a baseline
    Given I am a user+
    And I go to edit baseline page    1
    When I fill in baseline form    B1Mod    1
    And I save
    Then Page Should Contain    Baseline edited successfully
    And I check baseline view form    B1Mod    2
    And Click link    a_features
    And I am waiting for ajax request
    And Xpath Should Match X Times    //a[text()='B1Mod']    2
    And Click link    a_main
    And I click button    Return to release
    And the table should contain x rows    3
    And the table should contain    B1Mod    Details 4 B1Mod

A manager closes a baseline
    Given I am a manager
    And I go to edit baseline page    1
    When Select Checkbox    map3_baseline_closed
    And I save
    Then Page Should Contain    Baseline edited successfully
    And Checkbox Should Be Selected    map3_baseline_closed
    And Click element    id=a_references
    And I should not see action button(s)    Add    Edit reference #1    Delete reference #1

A manager opens a closed baseline
    Given I am a manager
    And I go to edit baseline page    3
    When Unselect Checkbox    map3_baseline_closed
    And I save
    Then Page Should Contain    Baseline edited successfully
    And Checkbox Should Not Be Selected    map3_baseline_closed
    And Click element    id=a_references
    And I should see action button(s)    Add    Edit reference #6    Delete reference #6

Check context for baseline edit with clean context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for baseline edit without context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    8
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role : ROLE_DM_USERPLUS    Update user

Check context for baseline edit with previous context
    [Tags]    context
    Given I am logging as    d2-manager    d2-manager    Hello    True
    And Go To    ${URL_DEV}/product/2
    And Go To    ${URL_DEV}/release/4
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    User->setCurrentProduct    Role by default: Guest    Update user

Check context for baseline edit with same previous product
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/2
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    7
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    User->setCurrentRelease    Same product. No change    Update user

Check context for baseline edit with same previous release
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/2
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    6
    And the context log file should contain    CoreController->setCurrentBaseline    User->setCurrentBaseline    Same release. No change    Update user

Check context for baseline edit with same previous context
    [Tags]    context
    Given I am logging as    d1-user+    d1-user+    Hello    True
    And Go To    ${URL_DEV}/product/1
    And Go To    ${URL_DEV}/release/1
    And Go To    ${URL_DEV}/baseline/1
    And I reset the context log file
    When Go To    ${URL_DEV}/baseline/edit/1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page should contain    Userd1-user+ (User+)
    And the context log file should contain x lines    4
    And the context log file should contain    CoreController->setCurrentBaseline    Same baseline. No change
    And the context log file should not contain    Update user

Impossible to edit a baseline with a name too short
    Given I am a manager
    And I go to edit baseline page    1
    When Input Text    map3_baseline_name    X
    And I save
    Then Page Should Contain    This value is too short. It should have 2 characters or more.

Impossible to modify a baseline with wrong date
    Given I am a manager
    And I go to edit baseline page    3
    When Input Text    //div[@data-link-field='map3_baseline_baselineDatetime']/input[@type='text']    xx/xx/xxxx
    And Click Element    map3_baseline_name
    And I save
    ${d}    Get Time    day
    Then Disabled text element should contain    map3_baseline_baselineDatetime    2014-08-02 12:00

Wrong baseline Id for a release but same product
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/4
    Then Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}
    And Page Should Contain    Release Two

Wrong baseline Id for a release
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/6
    Then Page Should Contain    ${FORBIDDEN_ACCESS_TEXT}

Wrong baseline Id
    Given I am a user+
    And I go to baselines list    View product #1    View release #1
    When Go to    ${URL}/baseline/edit/999
    Then I should see 404 error page

Edit a baseline without selecting a product before
    Given I am a user+
    When Go to    ${URL}/baseline/edit/1
    Then Location should be    ${URL}/baseline/edit/1
    And the breadcrumb should contain    Product One    Release One    Baseline One    Edit
    And Page Should Not Contain    ${FORBIDDEN_ACCESS_TEXT}

Breadcrumb
    Given I am a user+
    When I go to edit baseline page    1
    Then the breadcrumb should contain    Product One    Release One    Baseline One    Edit

Breadcrumb product link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl1
    Then Location Should Be    ${URL}/product/1

Breadcrumb release link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl2
    Then Location Should Be    ${URL}/release/1

Breadcrumb baseline link
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=br_lvl3
    Then Location Should Be    ${URL}/baseline/1

Tab Main
    Given I am a user+
    And I go to edit baseline page    1
    When Click Link    id=a_main
    And Location Should Be    ${URL}/baseline/1

Tab Features
    [Tags]    failedTest
    Given I am a user+
    And I go to edit baseline page    1
    And Element Should Contain    id=badge_features    XX
    When Click element    id=a_features
    And Location Should Be    ${URL}/bln-features/1

Tab References
    [Tags]    failedTest
    Given I am a user+
    And I go to edit baseline page    1
    And Element Should Contain    id=badge_references    3
    When Click element    id=a_references
    And Location Should Be    ${URL}/bln-refs/1

Return to release button
    Given I am a user+
    And I go to edit baseline page    1
    When I click button    Return to release
    Then Location Should Be    ${URL}/baselines/1

*** Keywords ***
I go to edit baseline page
    [Arguments]    ${id}
    I go to baselines list    View product #1    View release #1
    I click action button    Edit baseline #${id}
